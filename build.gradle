apply from: 'dependencies.gradle'

buildscript {
    ext.kotlinVersion = '1.3.40'
    ext.androidGradlePlugin = '3.4.1'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {

            android {
                compileSdkVersion androidCompileSdk
                buildToolsVersion androidBuildTools

                defaultConfig {
                    minSdkVersion androidMinSdk
                    targetSdkVersion androidTargetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility javaVersion
                    targetCompatibility javaVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
